#!/usr/bin/env python
import os
import sys

# Check ORC framework source path (shared sources)
orc_path = os.path.abspath("../../../addons/open-render-craft/gdextension")
if not os.path.exists(orc_path):
    print("ERROR: ORC framework sources not found at:", orc_path)
    Exit(1)

env = SConscript(f"{orc_path}/godot-cpp/SConstruct")

env.Append(CPPPATH=[
    "src/public",
    f"{orc_path}/src/public",
])

# TODO might not need this define now we do not rely on .dll dependency
env.Append(CPPDEFINES=['ORC_RENDERER_EXPORTS'])

env['OBJSUFFIX'] = '.o'

sources = []

# Include ORC src but exclude the register_types
orc_src_dir = f"{orc_path}/src/private"
orc_sources = Glob(os.path.join(orc_src_dir, "*.cpp"))
orc_sources = [src for src in orc_sources if 'register_types' not in str(src)]

sources += orc_sources

src_dir = "src/private"
obj_dir = "obj"
VariantDir(obj_dir, src_dir, duplicate=0)
sources += Glob(os.path.join(obj_dir, "*.cpp"))

library = env.SharedLibrary(
    "bin/liborcpotatocppimpl{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
    source=sources,
)

Default(library)
